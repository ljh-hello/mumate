Outputformat=OVF2_TEXT
//	Material properties	//

Aex = 3.05E-13 //exchange stiffness (J/m)	
D:= 2.75E-4 //
Dbulk= D //Bulk Dzyaloshinskii-Moriya strength (J/m^2)
Msat= 160E3 //Saturation magnetisation (A/m)
Ku1= -0.8E4 //1st order uniaxial anisotropy (J/m^3)
anisU=vector(1,0,0) //uniaxial anisotropy direction
alpha= 0.1 //Landau-Lifshitz damping constant

//	//	//	//	//	//

//	Define geometry and cell size	//

H:= 18E-9 //sample thickness (m)

//number of cells in x,y,z
Nx:= 2
Ny:= 2
Nz:= 128

//X, Y, Z cell sizes in meters
X:=H/Nx
Y:=H/Ny
Z:=H/Nz

SetGridSize(Nx, Ny, Nz)	//set grid sizes
SetCellSize(X,Y,Z)	//set cell sizes
setPBC(128,128,0)	//periodic boundary conditions on x,y, 128 reps
			//of the demag field along x and y

//	//	//	//	//	//

//	Define magnetisation and magnetic fields	//

EnableDemag=True //turn off for now, will reenable when have data from initial run

m.Loadfile("helicoidzerofield22128.ovf")
Bx:=0.0	//Defines the x,y,z components of B
By:=0.0
Bz:=0.0
Bx_max:=1.0
By_max:=1.0
Bz_max:=1.0

amp:=10e-2	//amplitude of EM pulse, 10^-2 Tesla
freq:=1.4985e11	//freq term inside sinc for microwave range 1.4985*10^11 Hz

//	//	//	//	//	//	//


//	Iterate through mag of ext B-field, apply pulse	//
for i:=0; i<=Bx_max; i+=0.1{

	B_ext=Vector(i,By,Bz)	//defines the vector B	
	minimize()		//evolves the energy to the minimum energy state

	for j:=0; j<=796; j++{	//796 repetitions at 5e-13 takes from 0 to 398ps
				//this is when the sinc fctn is 0 after local min
				//10 wavelengths
		t:=j*5E-13
		Bx=i
		By=sqrt(2/pi)*sinc(freq*(t-2.096E-11))*amp
		save(m)
		save(B_ext)
		B_ext=Vector(Bx, By, Bz)
		run(5E-13)
	}
}
//	//	//	//	//	//	//
