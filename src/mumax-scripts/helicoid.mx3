Outputformat=OVF2_TEXT
//	Material properties	//

Aex = 3.05E-13 //exchange stiffness (J/m)	
D:= 2.75E-4 //
Dbulk= D //Bulk Dzyaloshinskii-Moriya strength (J/m^2)
Msat= 160E3 //Saturation magnetisation (A/m)
Ku1= -0.8E4 //1st order uniaxial anisotropy (J/m^3)
anisU=vector(1,0,0) //uniaxial anisotropy direction
alpha= 0.1 //Landau-Lifshitz damping constant

//	//	//	//	//	//

//	Define geometry and cell size	//

H:= 18E-9 //sample thickness (m)

//number of cells in x,y,z
Nx:= 2
Ny:= 2
Nz:= 128
N:=Nz //cells have to be cubic for demag kernel to work?
//X, Y, Z cell sizes in meters
X:=H/N
Y:=H/N
Z:=H/N

SetGridSize(Nx, Ny, Nz)	//set grid sizes
SetCellSize(X,Y,Z)	//set cell sizes
setPBC(256,256,0)	//periodic boundary conditions on x,y, 256 reps
			//of the demag field along x and y

//	//	//	//	//	//

//	Define magnetisation and magnetic fields	//

EnableDemag=True //turn off for now, will reenable when have data from initial run

Bx:=0.0	//Defines the x,y,z components of B
By:=0.0
Bz:=0.0
Bx_max:=1.0
By_max:=1.0
Bz_max:=1.0

amp:=10e-5	//amplitude of EM pulse, 10^-5 Tesla
freq:=2e11	//freq term inside sinc for microwave range 2*10^11 Hz

//	//	//	//	//	//	//


//	Iterate through mag of ext B-field, apply pulse	//

for idx:=0; idx<=20*Bx_max; idx++{
	i:=idx/20 
	Bx=i*0.77	//since we have h/HD=i and HD~0.77T
	By=0.0		//resets the y comp of the B field
	if i<0.45{	//load the pre critical field helicoid
		m.Loadfile("helicoid1.ovf")

	}else{	//when h/HD is at or above the critical field, load the post crit field helicoid	
		m.Loadfile("helicoid2.ovf")
	}
	B_ext=Vector(Bx,By,Bz)	//defines the vector B	
	minimize()		//evolves the energy to the minimum energy state
	save(m)
	for j:=0; j<=880; j++{	//880 repetitions at 5e-13 takes from 0 to 440ps
				//this is when the sinc fctn is 0 after local min
				//10 wavelengths
		t:=j*5E-13
		By=sqrt(2/pi)*sinc(freq*t)*amp
		B_ext=Vector(Bx, By, Bz)
		run(5E-13)
		save(m)
	}
}
//	//	//	//	//	//	//
